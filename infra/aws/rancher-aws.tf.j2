
terraform {
  required_providers {
    aws = {
      source = "opentofu/aws"
      version = "6.20.0"
    }
  }
}

provider "aws" {
  alias   = "us-east-1"
  region  = "us-east-1"
  #profile = "us-east-1-profile"
}

data "aws_ami" "rhel9" {
  most_recent = true
  # uncomment for commercial owner ID
  owners = ["309956199498"]

  filter {
    name   = "name"
    values = ["RHEL-9.3*"]
  }

  filter {
    name   = "architecture"
    values = ["x86_64"]
  }
}

# Create a VPC for the Bootstrap cluster
resource "aws_vpc" "bootstrap_vpc" {
  provider             = aws.us-east-1
  cidr_block           = "10.0.0.0/16"
  enable_dns_support   = true
  enable_dns_hostnames = true

  tags = {
    Name = "bootstrap-cluster-vpc"
  }
}

# Create an Internet Gateway for the VPC
resource "aws_internet_gateway" "bootstrap_igw" {
  provider = aws.us-east-1
  vpc_id   = aws_vpc.bootstrap_vpc.id

  tags = {
    Name = "bootstrap-cluster-igw"
  }
}

# Create a route table for the public subnet
resource "aws_route_table" "bootstrap_rt" {
  provider = aws.us-east-1
  vpc_id = aws_vpc.bootstrap_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.bootstrap_igw.id
  }
  tags = {
    Name = "bootstrap-cluster-rt"
  }
}

# Create a public subnet for the Bootstrap cluster
resource "aws_subnet" "bootstrap_subnet" {
  provider          = aws.us-east-1
  vpc_id            = aws_vpc.bootstrap_vpc.id
  cidr_block        = "10.0.1.0/24"
  availability_zone = "us-east-1a"

  tags = {
    Name = "bootstrap-cluster-subnet"
  }
}

# Associate the route table with the public subnet
resource "aws_route_table_association" "bootstrap_rta" {
  provider       = aws.us-east-1
  subnet_id      = aws_subnet.bootstrap_subnet.id
  route_table_id = aws_route_table.bootstrap_rt.id
}

# Create a security group for the Bootstrap cluster
resource "aws_security_group" "bootstrap_sg" {
  provider    = aws.us-east-1
  name        = "bootstrap-cluster-sg"
  description = "Security group for Bootstrap cluster"
  vpc_id      = aws_vpc.bootstrap_vpc.id

  # Allow HTTPS from the internet
  ingress {
    from_port   = 443
    to_port     = 443
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  # Allow all traffic within the VPC
  ingress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = [aws_vpc.bootstrap_vpc.cidr_block]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
  tags = {
    Name = "bootstrap-cluster-sg"
  }
}

# Create an EC2 instance for the Bootstrap cluster
resource "aws_instance" "bootstrap_node" {
  provider                    = aws.us-east-1
  ami                         = data.aws_ami.rhel9.image_id
  instance_type               = "c6id.xlarge"
  subnet_id                   = aws_subnet.bootstrap_subnet.id
  vpc_security_group_ids      = [aws_security_group.bootstrap_sg.id]
  associate_public_ip_address = true
  user_data                   = file("../../infra/cloud-init/bootstrap/cloud-init-aws.yml")
  root_block_device {
    volume_size = 60
  }

  tags = {
    Name = "bootstrap-cluster-node"
  }
}

# Update the hostname in the rancher-helmchart.yml file before the instance is created
resource "null_resource" "update_helmchart" {
  triggers = {
    always_run = timestamp()
  }

  provisioner "local-exec" {
    command = <<EOT
      sed -i "s/hostname: .*/hostname: ${aws_instance.bootstrap_node.public_dns}/" ../../cluster-gitops/rancher-cluster/rancher/rancher-helmchart.yml
    EOT
  }
}

# Update the hostname in the cloud-init-aws.yml file before the instance is created
resource "null_resource" "update_cloudinit" {
  triggers = {
    always_run = timestamp()
  }

  provisioner "local-exec" {
    command = <<EOT
      sed -i "s/hostname: .*/hostname: ${aws_instance.bootstrap_node.public_dns}/" ../../infra/cloud-init/bootstrap/cloud-init-aws.yml
    EOT
  }
}

# Output the public IP of the Bootstrap node
output "bootstrap_node_public_ip" {
  value = aws_instance.bootstrap_node.public_ip
}

# Output the public DNS name of the Bootstrap node
output "bootstrap_node_public_dns" {
  value = aws_instance.bootstrap_node.public_dns
}

